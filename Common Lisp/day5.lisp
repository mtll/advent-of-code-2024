(defpackage-plus-1:defpackage+ #:aoc2024.day5
  (:use #:cl)
  (:local-nicknames (#:pre #:cl-ppcre))
  (:export #:solve))

(in-package #:aoc2024.day5)

(defun solve (path &aux (table (make-hash-table)))
  (destructuring-bind (rules lines)
      (cl-ppcre:split "\\n\\n" (uiop:read-file-string path))
    (pre:do-register-groups ((#'parse-integer a) (#'parse-integer b))
        ("(\\d+)\\|(\\d+)" rules)
      (push a (gethash b table)))
    (let ((ord (lambda (a b) (member b (gethash a table))))
          (part1 0)
          (part2 0))
      (pre:do-matches-as-strings (line ".*\\n" lines (values part1 part2))
        (let ((sorted nil)
              (unsorted nil))
          (pre:do-matches-as-strings (a "\\d+" line)
            (push (parse-integer a) unsorted))
          (setf sorted (sort (copy-seq unsorted) ord))
          (if (equal unsorted sorted)
              (incf part1 (nth (floor (length unsorted) 2) unsorted))
              (incf part2 (nth (floor (length sorted) 2) sorted))))))))
